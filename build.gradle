buildscript {
    ext {
        springBootVersion = "2.0.6.RELEASE"
        spockVersion = "1.2-groovy-2.5"
        // https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all
        groovyVersion = "3.0.10"
        // https://projectlombok.org/changelog
        lombokVersion = "1.18.24"
        dockerComposePluginVersion = "0.16.4"
    }
    repositories {
        mavenCentral()
        // jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.github.tntim96:fakesmtp:2.0" // ローカルでダミーのSMTPサーバーを立てる
        classpath "com.avast.gradle:gradle-docker-compose-plugin:${dockerComposePluginVersion}"
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 11
    targetCompatibility = 11
    [compileJava, compileTestJava, compileGroovy, compileTestGroovy]*.options*.encoding = "UTF-8"
    [compileJava, compileTestJava, compileGroovy, compileTestGroovy]*.options*.compilerArgs = ["-Xlint:all"]

    sourceSets {
        test.resources {
            // テスト時にsrc/main/resourcesにある設定ファイルを使用する
            srcDirs "src/main/resources"
            srcDirs "src/test/resources"
        }
    }

    repositories {
        mavenCentral()
        // jcenter()

        // jasperreports
        // https://b1san-blog.com/post/spring/spring-jasperreports/
        // maven { url "https://jasperreports.sourceforge.net/maven2/" }
        maven { url "https://jaspersoft.jfrog.io/jaspersoft/third-party-ce-artifacts/" }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true

            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }

    eclipse {
        classpath {
            containers.remove("org.eclipse.jdt.launching.JRE_CONTAINER")
            containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-11"
        }
    }

    bootRun {
        sourceResources sourceSets.main
        jvmArgs "-XX:TieredStopAtLevel=1", "-Xverify:none"
    }

    ext["groovy.version"] = groovyVersion
    
    dependencies {
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

        testImplementation "org.assertj:assertj-core"
        testImplementation "org.spockframework:spock-core:${spockVersion}"
        testImplementation "org.mockito:mockito-core"
    }
}

task startFakeSmtpServer(type: JavaExec) {
    classpath = buildscript.configurations.classpath
    args = ["--start-server", "-m"] // -m (memory-mode)
    // https://docs.gradle.org/7.4.2/dsl/org.gradle.api.tasks.JavaExec.html#org.gradle.api.tasks.JavaExec:main
    mainClass = "com.nilhcem.fakesmtp.FakeSMTP"
}

apply plugin: "docker-compose"

dockerCompose {
    useComposeFiles = ["docker/docker-compose.yml"]
}

apply plugin: com.sample.CodeGenPlugin

codegen {
    domainProjectName = "sample-domain"
    webProjectName = "sample-web-admin"

    commonDtoPackageName = "com.sample.domain.dto.common"
    daoPackageName = "com.sample.domain.dao"
    dtoPackageName = "com.sample.domain.dto"
    repositoryPackageName = "com.sample.domain.repository"
    servicePackageName = "com.sample.domain.service"
    commonServicePackageName = "com.sample.domain.service"
    exceptionPackageName = "com.sample.domain.exception"
    webBasePackageName = "com.sample.web.base"
    baseValidatorPackageName = "com.sample.domain.validator"
    baseControllerPackageName = "com.sample.web.base.controller.html"
    controllerPackageName = "com.sample.web.admin.controller.html"
}

// https://qiita.com/Yu-s/items/13a6a8db8dc191bb3b42
// task wrapper(type: Wrapper) {
wrapper {
    gradleVersion = "7.4.2"
}
